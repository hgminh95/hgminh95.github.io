---
layout: post
title:  "Graph"
date:   2015-07-28
parent: "cl-2016"
tag:
- algorithm
- cl
comments: true
---

## BFS

### Tham khảo

![bfs](http://ben.com/misc/bfs-anim-obstacle.gif)


### Code

{% highlight pascal %}
program ideone;
const MAX_N = 1000;
      hx: array[1..4] of longint = (1, -1, 0, 0);
      hy: array[1..4] of longint = (0, 0, 1, -1);
var n, m: longint;
    d, a: array[1..MAX_N, 1..MAX_N] of longint;
    qx, qy: array[1..MAX_N * MAX_N] of longint;
    l, r: longint;
    i, j: longint;
    sx, sy, ex, ey: longint;

procedure init();
var i, j: longint;
begin
    // Khởi tạo mảng d là khoảng cách ngắn nhất đến ô (i, j)
    for i:= 1 to n do
        for j:= 1 to m do d[i][j]:= -1;
    r:= -1;
    l:= 0;
end;

procedure push(x, y: longint);
begin;
    r:= r + 1;
    qx[r]:= x;
    qy[r]:= y;
end;

procedure bfs(x, y: longint);
var i, ux, uy, vx, vy: longint;
begin
    d[x][y]:= 0;
    push(x, y);

    // Trong khi queue vẫn còn phần tử
    while (l <= r) do
    begin
        // Lấy phần tử đầu tiên và gán vào (ux, uy)
        ux:= qx[l];
        uy:= qy[l];
        l:= l + 1;

        // Duyệt tất cả các ô kề cạnh của (ux, uy)
        for i:= 1 to 4 do
        begin
            vx:= ux + hx[i];
            vy:= uy + hy[i];

            // Kiểm tra (vx, vy) có thuộc lưới ô vuông hay không
            if (vx <= 0) or (vy <= 0) or (vx > n) or (vy > m) then continue;

            // Kiểm tra xem ô (vx, vy) đã đưọc loang chưa và có phải là
            // vật cản không.
            if (d[vx][vy] = -1) and (a[vx][vy] = 0) then
            begin
                d[vx][vy]:= d[ux][uy] + 1;
                push(vx, vy);
            end;
        end;
    end;
end;
begin
    readln(n, m);
    for i:= 1 to n do
        for j:= 1 to m do
            read(a[i][j]);
    readln(sx, sy);
    readln(ex, ey);

    init();
    bfs(sx, sy);
    writeln(d[ex][ey]);
end.
{% endhighlight %}
